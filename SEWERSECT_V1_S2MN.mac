! Get corner nodes to convert stresses to internal forces
CMSEL,S,COMPN_Sewer

N10=NODE(X4,Y2,0)
N11=NODE(X3,Y1,0)
N12=NODE(X3,Y2,0)
N13=NODE(X4,Y1,0)

N20=NODE(X5,Y2,0)
N21=NODE(X6,Y1,0)
N22=NODE(X6,Y2,0)
N23=NODE(X5,Y1,0)

N30=NODE(X4,Y3,0)
N31=NODE(X3,Y4,0)
N32=NODE(X3,Y3,0)
N33=NODE(X4,Y4,0)

N40=NODE(X5,Y3,0)
N41=NODE(X6,Y4,0)
N42=NODE(X6,Y3,0)
N43=NODE(X5,Y4,0)

! Fictional cross-sections: geometric properties
secProp_I=(SewerWallThk**3)/12
secProp_W=secProp_I/(SewerWallThk/2)
secProp_A=SewerWallThk

! Number of shear force cross-sections
NSEL,S,LOC,X,X4,X5
NSEL,R,LOC,Y,Y1
*get,VSEC_horCount,NODE,,COUNT
NSEL,S,LOC,X,X4
NSEL,R,LOC,Y,Y2,Y3
*get,VSEC_verCount,NODE,,COUNT

! Bottom slab
PTHS2MN,N22,N12,N21,N11,'MN_Bot','X',200,secProp_W,secProp_A,0        ! 'LJ_INF'
PTHS2V,N10,N20,N13,N23,'V_Bot','XY',VSEC_horCount,,secProp_A,0

! Top slab
PTHS2MN,N32,N42,N31,N41,'MN_Top','X',200,secProp_W,secProp_A,0       ! 'LJ_SUP'
PTHS2V,N33,N43,N30,N40,'V_Top','XY',VSEC_horCount,,secProp_A,0

! Left wall
PTHS2MN,N13,N33,N11,N31,'MN_Lft','Y',200,secProp_W,secProp_A,0         ! 'PR_ESQ'
PTHS2V,N12,N32,N10,N30,'V_Lft','XY',VSEC_verCount,,secProp_A,0

! Right wall
PTHS2MN,N43,N23,N41,N21,'MN_Rgt','Y',200,secProp_W,secProp_A,0        ! 'PR_DIR'
PTHS2V,N20,N40,N22,N42,'V_Rgt','XY',VSEC_verCount,,secProp_A,0

! Display settings
/annot,DEFA
/annot,TMOD,1
/device,FONT,3,ARIAL,400,0,-21
/annot,ON
/annot,SCALE,1
/tspec,15,100,100
/annot,DELE
ALLSEL

! Plot bending moments
baseScale=10
*del,MULTIPATHPLOT_ARR_pathNames
*del,MULTIPATHPLOT_ARR_labels
*del,MULTIPATHPLOT_ARR_parts
*dim,MULTIPATHPLOT_ARR_pathNames,ARRAY,4
*dim,MULTIPATHPLOT_ARR_labels,ARRAY,4
*dim,MULTIPATHPLOT_ARR_parts,ARRAY,4
MULTIPATHPLOT_ARR_pathNames(1)=   'MN_Bot',   'MN_Top',   'MN_Lft',   'MN_Rgt'
MULTIPATHPLOT_ARR_labels(1)=           'M',        'M',        'M',        'M'
MULTIPATHPLOT_ARR_parts(1)=     'Bot Slab', 'Top Slab', 'Lft Wall', 'Rgt Wall'
MULTIPATHPLOT, 'CULVERT_M', baseScale, 0.8, -0.70

! Plot shear forces
baseScale=10
*del,MULTIPATHPLOT_ARR_pathNames
*del,MULTIPATHPLOT_ARR_labels
*del,MULTIPATHPLOT_ARR_parts
*dim,MULTIPATHPLOT_ARR_pathNames,ARRAY,4
*dim,MULTIPATHPLOT_ARR_labels,ARRAY,4
*dim,MULTIPATHPLOT_ARR_parts,ARRAY,4
MULTIPATHPLOT_ARR_pathNames(1)=    'V_Bot',    'V_Top',    'V_Lft',    'V_Rgt'
MULTIPATHPLOT_ARR_labels(1)=           'V',        'V',        'V',        'V'
MULTIPATHPLOT_ARR_parts(1)=     'Bot Slab', 'Top Slab', 'Lft Wall', 'Rgt Wall'
MULTIPATHPLOT, 'CULVERT_V', baseScale, 0.8, -0.70










